buildscript {
    apply from: rootProject.file("gradle/versions.gradle")

    repositories {
        google()
        jcenter()
    }

    dependencies {
        print "Gradle version: $rootProject.ext.gradleVersion"
        classpath "com.android.tools.build:gradle:$rootProject.ext.gradleVersion"
        classpath "net.serenity-bdd:serenity-gradle-plugin:1.9.6"
    }
}

allprojects {
    // Windows has a maximum path length of 260 characters - to avoid hitting this ceiling
    // stage the builds close to the root of C://
    if (org.gradle.internal.os.OperatingSystem.current().isWindows()) {
        buildDir = "C:/temp/${rootProject.name}/${project.name}"
    }
    repositories {
        google()
        jcenter()
         // Adding here because Travis cannot access AndroidADAL feed.
        maven {
            url 'https://pkgs.dev.azure.com/MicrosoftDeviceSDK/DuoSDK-Public/_packaging/Duo-SDK-Feed%40Local/maven/v1'
            name 'Duo-SDK-Feed@Local'
        }
    }

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked"
        }
    }

    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}


task clean(type: Delete) {
    delete rootProject.buildDir
}
