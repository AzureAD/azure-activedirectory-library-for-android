project.ext.vstsUsername = System.getenv("ENV_VSTS_MVN_ANDROID_MSAL_USERNAME") != null ? System.getenv("ENV_VSTS_MVN_ANDROID_MSAL_USERNAME") : project.findProperty("vstsUsername")
project.ext.vstsMavenAccessToken = System.getenv("ENV_VSTS_MVN_ANDROIDADACCOUNTS_ACCESSTOKEN") != null ? System.getenv("ENV_VSTS_MVN_ANDROIDADACCOUNTS_ACCESSTOKEN") : project.findProperty("vstsMavenAccessToken")
buildscript {
    apply from: rootProject.file("gradle/versions.gradle")

    // Authenticator is setting versions of their dependencies in their gradle.properties file.
    // We'll load and merge the values we don't have in our properties file.
    Properties props = new Properties()
    props.load(new FileInputStream("authenticator/PhoneFactor/gradle.properties"))
    props.each { prop ->
        if (!project.ext.has(prop.key)) {
            project.ext.set(prop.key, prop.value)
        }
    }
    
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:${rootProject.ext.gradleVersion}"
        classpath "net.serenity-bdd:serenity-gradle-plugin:1.9.6"
        classpath 'com.google.gms:google-services:3.2.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:${project.ext.get('androidx_navigation_version')}"
    }
}

allprojects {
    if (org.gradle.internal.os.OperatingSystem.current().isWindows()) {
        buildDir = "C:/temp/${rootProject.name}/${project.name}"
    }

    repositories {
        mavenLocal()
        maven {
            // msazure and aria are consumed via upstream sources of the AndroidADAL feed.
            name "vsts-maven-adal-android"
            url "https://identitydivision.pkgs.visualstudio.com/_packaging/AndroidADAL/maven/v1"
            credentials {
                username project.vstsUsername
                password project.vstsMavenAccessToken
            }
        }
        google()
        jcenter()
    }

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked"
        }
    }

    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
if (!project.hasProperty("vstsUsername")) {
    logger.error('vstsUsername is missing in gradle.properties')
}
if (!project.hasProperty("vstsMavenAccessToken")) {
    logger.error('vstsMavenAccessToken is missing in gradle.properties')
}
