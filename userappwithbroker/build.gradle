apply plugin: 'com.android.application'

allprojects {
    repositories {
        mavenLocal()
        google()
        maven {
            name "vsts-maven-adal-android"
            url "https://identitydivision.pkgs.visualstudio.com/_packaging/AndroidADAL/maven/v1"
            credentials {
                username System.getenv("ENV_VSTS_MVN_ANDROIDADAL_USERNAME") != null ? System.getenv("ENV_VSTS_MVN_ANDROIDADAL_USERNAME") : project.findProperty("vstsUsername")
                password System.getenv("ENV_VSTS_MVN_ANDROIDADAL_ACCESSTOKEN") != null ? System.getenv("ENV_VSTS_MVN_ANDROIDADAL_ACCESSTOKEN") : project.findProperty("vstsMavenAccessToken")
            }
        }
        jcenter()
    }
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        applicationId "com.microsoft.aad.adal.userappwithbroker"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
    }

    signingConfigs {
        debug {
            def debugKeyFile = rootProject.file("gradle/debug.keystore")
            if(debugKeyFile.exists()){
                storeFile rootProject.file("gradle/debug.keystore")
                storePassword "android"
                keyAlias "androiddebugkey"
                keyPassword "android"
            }
        }

        release {
            def debugKeyFile = rootProject.file("gradle/debug.keystore")
            if(debugKeyFile.exists()){
                storeFile rootProject.file("gradle/debug.keystore")
                storePassword "android"
                keyAlias "androiddebugkey"
                keyPassword "android"
            }
        }
    }

    flavorDimensions 'main'

    productFlavors {
        local{

        }
        dist{

        }
    }

    buildTypes {
        debug {
            minifyEnabled true
            buildConfigField("String", "REGULAR_REDIDRECT_URI", "\"msauth://com.microsoft.aad.adal.userappwithbroker/1wIqXSqBj7w%2Bh11ZifsnqwgyKrY%3D\"")
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'userapp-rules.pro'
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'userapp-rules.pro'
            buildConfigField("String", "REGULAR_REDIDRECT_URI", "\"msauth://com.microsoft.aad.adal.userappwithbroker/1wIqXSqBj7w%2Bh11ZifsnqwgyKrY%3D\"")
        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    // Compile Dependencies
    implementation "com.google.android.material:material:$rootProject.ext.materialVersion"

    localImplementation(project(':adal'))
    distImplementation("com.microsoft.aad:adal:3.1.0") {
        exclude module: 'support-v4'
        exclude module: 'android'
    }
}
